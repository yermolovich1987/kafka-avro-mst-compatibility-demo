buildscript {
	repositories {
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
		maven {
			url 'http://packages.confluent.io/maven/'
		}
		// Required for newer version of the 'kafka-schema-registry-gradle-plugin'
		maven {
			url = uri("https://jitpack.io")
		}
	}
}

plugins {
	id 'org.springframework.boot'
	id 'io.spring.dependency-management'
	id 'com.github.davidmc24.gradle.plugin.avro'
	id 'com.github.imflog.kafka-schema-registry-gradle-plugin'
	id 'com.avast.gradle.docker-compose'
	id 'java'
}

sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

sourceSets {
	main {
		java {
			srcDir "${buildDir.absolutePath}/build/generated-main-avro-java"
		}
	}
}

repositories {
	mavenCentral()
	maven {
		url 'http://packages.confluent.io/maven/'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'io.confluent:kafka-avro-serializer'
	compile 'org.apache.avro:avro'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
}

test {
	useJUnitPlatform()
}

avro {
	fieldVisibility = "PRIVATE"
	// Required to eliminate known bug with schema compatibility => https://github.com/confluentinc/schema-registry/issues/868
	stringType = "CharSequence"
}

schemaRegistry {
	url = "http://localhost:8081"
	register {
		// Start of the multi-schema definition section.
		subject('multi-schema-topic-com.demo.avro.TaskCreated', 'demo-kafka-app/src/main/avro/TaskCreated.avsc')
		subject('multi-schema-topic-com.demo.avro.TaskCancelled', 'demo-kafka-app/src/main/avro/TaskCancelled.avsc')
	}
	config {
		subject('multi-schema-topic-com.demo.avro.TaskCreated', 'FULL_TRANSITIVE')
		subject('multi-schema-topic-com.demo.avro.TaskCancelled', 'FULL_TRANSITIVE')
	}
}

task runDockerComposeWithSchemas {
	dependsOn 'composeUp'
	dependsOn 'registerSchemasTask'
	dependsOn 'configSubjectsTask'
	tasks.findByName('registerSchemasTask').mustRunAfter 'composeUp'
	tasks.findByName('configSubjectsTask').mustRunAfter 'registerSchemasTask'
}

dockerCompose {
	forceRecreate = true
}