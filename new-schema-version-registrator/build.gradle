buildscript {
	repositories {
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
		maven {
			url 'http://packages.confluent.io/maven/'
		}
		// Required for newer version of the 'kafka-schema-registry-gradle-plugin'
		maven {
			url = uri("https://jitpack.io")
		}
	}
}

plugins {
	id 'io.spring.dependency-management'
	id 'com.github.davidmc24.gradle.plugin.avro'
	id 'com.github.imflog.kafka-schema-registry-gradle-plugin'
	id 'java'
}

sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

sourceSets {
	main {
		java {
			srcDir "${buildDir.absolutePath}/build/generated-main-avro-java"
		}
	}
}

repositories {
	mavenCentral()
	maven {
		url 'http://packages.confluent.io/maven/'
	}
}

dependencies {
	compile 'org.apache.avro:avro'
}

test {
	useJUnitPlatform()
}

avro {
	fieldVisibility = "PRIVATE"
	// Required to eliminate known bug with schema compatibility => https://github.com/confluentinc/schema-registry/issues/868
	stringType = "CharSequence"
}

schemaRegistry {
	url = 'http://localhost:8081'
	register {
		subject('simple-single-message-topic-value', 'new-schema-version-registrator/src/main/avro/SimpleSingleMessage.avsc')
		subject('task-created-avro-value', 'new-schema-version-registrator/src/main/avro/TaskCreated.avsc')
		subject('task-cancelled-avro-value', 'new-schema-version-registrator/src/main/avro/TaskCancelled.avsc')
		subject('multi-schema-topic-value', 'new-schema-version-registrator/src/main/avro/TaskEvent.avsc')
				.addReference('task-created-avro-value', 'task-created-avro-value', -1)
				.addReference('task-cancelled-avro-value', 'task-cancelled-avro-value', -1)
	}
	config {
		subject('simple-single-message-topic-value', 'FULL_TRANSITIVE')
		subject('task-created-avro-value', 'FULL_TRANSITIVE')
		subject('task-cancelled-avro-value', 'FULL_TRANSITIVE')
		// Sets the compatibility level for topic with schema reference.
		subject('multi-schema-topic-value', 'FULL_TRANSITIVE')
	}
}
